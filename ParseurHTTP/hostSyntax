Host-header = "Host" ":" OWS Host OWS

Host = uri-host [ ":" port ]

uri-host = host
port = *DIGIT

host = IP-literal / IPv4address / reg-name

IP-literal = "[" ( IPv6address / IPvFuture  ) "]"
IPv4address = dec-octet "." dec-octet "." dec-octet "." dec-octet
reg-name = *( unreserved / pct-encoded / sub-delims )

IPv6address   =                            6( h16 ":" ) ls32
              /                       "::" 5( h16 ":" ) ls32
              / [               h16 ] "::" 4( h16 ":" ) ls32
              / [ h16 *1( ":" h16 ) ] "::" 3( h16 ":" ) ls32
              / [ h16 *2( ":" h16 ) ] "::" 2( h16 ":" ) ls32
              / [ h16 *3( ":" h16 ) ] "::"    h16 ":"   ls32
              / [ h16 *4( ":" h16 ) ] "::"              ls32
              / [ h16 *5( ":" h16 ) ] "::"              h16
              / [ h16 *6( ":" h16 ) ] "::"
IPvFuture = "v" 1*HEXDIG "." 1*( unreserved / sub-delims / ":" )

dec-octet     = "25" %x30-35          ; 250-255
              / "2" %x30-34 DIGIT     ; 200-249
              / "1" 2DIGIT            ; 100-199
              / %x31-39 DIGIT         ; 10-99
              / DIGIT                 ; 0-9

unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded = "%" HEXDIG HEXDIG
sub-delims = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="

h16 = 1*4HEXDIG
ls32 = ( h16 ":" h16 ) / IPv4address
